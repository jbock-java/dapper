plugins {
    id('java')
    id('com.github.johnrengelman.shadow') version('7.1.2')
    id('maven-publish')
    id('signing')
}

group = 'io.github.jbock-java'

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.compilerArgs << '--release'
    options.compilerArgs << '11'
}

javadoc {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    google()
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(GenerateModuleMetadata) {
    enabled = true
}

shadowJar {
    minimize()
    archiveClassifier.set('')
}

dependencies {
    def dapper = project(':dapper')
    def javapoet = 'io.github.jbock-java:javapoet:1.15'
    def ac =  'io.github.jbock-java:auto-common:1.2.3'
    def graph = 'io.github.jbock-java:guava-graph:1.0.2'
    shadow(javapoet)
    shadow(ac)
    shadow(graph)
    shadow(dapper)
    annotationProcessor('io.github.jbock-java:dapper-compiler:1.2.1')
    implementation('androidx.room:room-compiler-processing:2.4.1')
    testImplementation(dapper)
    testImplementation(ac)
    testImplementation('io.github.jbock-java:compile-testing:0.19.12')
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.8.2')
    testImplementation(graph)
    testImplementation(javapoet)
}

jar {
    manifest {
        attributes(
                'Implementation-Version': project.properties['version']
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events('failed')
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

artifacts {
    archives(javadocJar, sourcesJar)
}

// https://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'dapper-compiler'

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'dapper-compiler'
                packaging = 'jar'
                description = 'dapper annotation processor'
                url = 'https://github.com/jbock-java/dapper'

                licenses {
                    license {
                        name = 'Apache 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Various'
                        name = 'Various'
                        email = 'jbock-java@gmx.de'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jbock-java/dapper.git'
                    developerConnection = 'scm:git:https://github.com/jbock-java/dapper.git'
                    url = 'https://github.com/jbock-java/dapper'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSS_USER')
                password = System.getenv('OSS_PASS')
            }
        }
    }
}

// https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
